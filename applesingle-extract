#!/usr/bin/env sh
exec poke -L "$0" "$@"
!#

load applesingle;

var suffixes = [
  .[1]  = "data",
  .[2]  = "rsrc",
  .[3]  = "real_name",
  .[4]  = "comment",
  .[5]  = "icon_bw",
  .[6]  = "icon_color",
  .[8]  = "file_dates",
  .[9]  = "finder_info",
  .[10] = "mac_info",
  .[11] = "prodos_info",
  .[12] = "msdos_info",
  .[13] = "short_name",
  .[14] = "afp_info",
  .[15] = "directory_id",
];

fun parse_entry_id = (string arg) int: {
  fun id_name_to_id = int: {
    for (id in AS_TYPES where arg == suffixes[id]) {
      return id;
    }
    raise E_inval;
  }

  try {
    return id_name_to_id;
  } catch if E_inval {
    return atoi(arg);
  }
}

fun suffix = (int id) string: {
  try {
    if (!(id in AS_TYPES)) { raise E_inval; }
    return suffixes[id];
  } catch {
    return ltos(id);
  }
}

var filename = argv[0];
var id = parse_entry_id(argv[1]);
var output = filename + "." + suffix(id);

printf "extracting entry %s to `%s'...\n", AS_TYPE_NAME[id], output;

var file = open(filename, IOS_M_RDONLY);

var header = AppleSingleHeader @ file : 0#B;

var stream = header.get_entry(id);

save :ios file :from stream._offset :size stream._len :file output;

printf "done\n";

// vim: ft=poke :
